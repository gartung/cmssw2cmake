macro(process_subdirs)
  set(SUBDIR_EXP "*")
  if(${ARGC} GREATER 1)
    set(SUBDIR_EXP ${ARGV})
  endif()
  file(GLOB subdirs ${SUBDIR_EXP})
  foreach(dir ${subdirs})
    if(IS_DIRECTORY ${dir} AND EXISTS ${dir}/CMakeLists.txt)
      get_filename_component(subdir ${dir} NAME)
      add_subdirectory(${subdir})
    endif()
  endforeach()
endmacro()

function(cms_add_interface name)
  set(singleValueArgs TYPE)
  set(multiValueArgs DEPS)
  cmake_parse_arguments(cms_add_interface "" "${singleValueArgs}" "${multiValueArgs}" ${ARGN})
  add_library(${name} INTERFACE)
  foreach(dep ${cms_add_interface_DEPS})
    cms_find_package(${dep})
    if(${dep}_FOUND)
      add_dependencies(${name} ${dep})
      target_link_libraries(${name} INTERFACE ${dep})
    endif()
    if(_LIBS)
      list(APPEND LIBS ${_LIBS})
      target_link_libraries(${name} INTERFACE ${_LIBS})
      unset(_LIBS)
    endif()  
  endforeach()
endfunction()

function(cms_add_library name)
  set(singleValueArgs TYPE)
  set(multiValueArgs SOURCES PUBLIC)
  message("Processing library ${name}")
  cmake_parse_arguments(cms_add_library "" "${singleValueArgs}" "${multiValueArgs}" ${ARGN})
  file(GLOB PRODUCT_SOURCES ${cms_add_library_SOURCES})
  file(GLOB PRODUCT_HEADERS "${CMAKE_CURRENT_BINARY_DIR}/interface/*.h*")
  set(${name}_EXTRA_SOURCES ${${name}_EXTRA_SOURCES} ${PRODUCT_HEADERS})
  add_library(${name} SHARED ${PRODUCT_SOURCES} ${${name}_EXTRA_SOURCES})
  target_include_directories(${name} PUBLIC "../../../")
  foreach(dep ${cms_add_library_PUBLIC})
    cms_find_package(${dep})
    if(${dep}_FOUND)
      add_dependencies(${name} ${dep})
      target_link_libraries(${name} ${dep})
    endif()
  endforeach()
  if ("${cms_add_library_TYPE}" STREQUAL "PLUGINS")
    SET_TARGET_PROPERTIES(${name} PROPERTIES PREFIX "plugin")
    edmplugingen(${name})
  endif()
  add_rootdict_rules(${name})
  if (EXISTS ${CMAKE_CURRENT_SOURCE_DIR}/headers.h)
    condformat_serialization(${name} headers.h)
  endif()
  if ("${cms_add_library_TYPE}" STREQUAL "TEST")
    set_target_properties(${name} PROPERTIES LIBRARY_OUTPUT_DIRECTORY ${CMAKE_SOURCE_DIR}/../test/${SCRAM_ARCH})
  else()
    set_target_properties(${name} PROPERTIES LIBRARY_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/lib)
    install(TARGETS ${name} DESTINATION lib/${SCRAM_ARCH})  
  endif()
endfunction()

function(cms_add_binary name)
  set(singleValueArgs TYPE)
  set(multiValueArgs SOURCES PUBLIC)
  message("Processing binary ${name}")
  cmake_parse_arguments(cms_add_binary "" "${singleValueArgs}" "${multiValueArgs}" ${ARGN})
  file(GLOB PRODUCT_SOURCES ${cms_add_binary_SOURCES})
  add_executable(${name} ${PRODUCT_SOURCES})
  target_include_directories(${name} PUBLIC "../../../")
  foreach(dep ${cms_add_binary_PUBLIC})
    cms_find_package(${dep})
    if(${dep}_FOUND)
        add_dependencies(${name} ${dep})
        target_link_libraries(${name} ${dep})
    endif()
    if(_LIBS)
      list(APPEND LIBS ${_LIBS})
      unset(_LIBS)
    endif()  
  endforeach()
  if("${cms_add_binary_TYPE}" STREQUAL "TEST")
    set_target_properties(${name} PROPERTIES RUNTIME_OUTPUT_DIRECTORY ${CMAKE_SOURCE_DIR}/../test/${SCRAM_ARCH})
  else()
    set_target_properties(${name} PROPERTIES RUNTIME_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/bin)
    install(TARGETS ${name} EXPORT cmssw DESTINATION bin)
  endif()
endfunction()

function(cms_add_test NAME)
  set(testdir ${CMAKE_SOURCE_DIR}/../test/${SCRAM_ARCH})
  set(singleValueArgs COMMAND)
  set(multiValueArgs TRARGS)
  message("Processing test ${NAME}")
  cmake_parse_arguments(cms_add_test "" "${singleValueArgs}" "${multiValueArgs}" ${ARGN})
  if(cms_add_test_TRARGS)
    add_test(NAME ${NAME} COMMAND ${cms_add_test_COMMAND} ${cms_add_test_TRARGS})
  else()
    add_test(NAME ${NAME} COMMAND ${cms_add_test_COMMAND})
  endif()
  cms_find_package(Runtime)
  set_tests_properties( ${NAME} PROPERTIES WORKING_DIR ${CMAKE_SOURCE_DIR}/.. )
  set_tests_properties( ${NAME} PROPERTIES ENVIRONMENT "CMSSW_SEARCH_PATH=$ENV{CMSSW_SEARCH_PATH};ROOT_GCC_TOOLCHAIN=$ENV{ROOT_GCC_TOOLCHAIN};ROOT_INCLUDE_PATH=$ENV{ROOT_INCLUDE_PATH};PYTHONPATH=${CMAKE_SOURCE_DIR}:$ENV{PYTHON27PATH}:$ENV{PYTHONPATH};PATH=${testdir}:${CMAKE_BINARY_DIR}/bin:$ENV{PATH};LD_LIBRARY_PATH=${testdir}:${CMAKE_BINARY_DIR}/lib:$ENV{LD_LIBRARY_PATH};LOCAL_TMP_DIR=${CMAKE_SOURCE_DIR}/..;LOCAL_TEST_DIR=${CMAKE_CURRENT_SOURCE_DIR}/;CMSSW_BASE=${CMAKE_SOURCE_DIR}/..;SCRAM_ARCH=${SCRAM_ARCH}")
endfunction()

macro(cms_find_package package)
if(EXISTS ${CMAKE_SOURCE_DIR}/../cmssw-cmake/coral/Find${package}.cmake)
#  if(NOT ${package}_FOUND)
    include(${CMAKE_SOURCE_DIR}/../cmssw-cmake/coral/Find${package}.cmake)
#  endif()
elseif(EXISTS ${CMAKE_SOURCE_DIR}/../cmssw-cmake/tools/Find${package}.cmake)
#  if(NOT ${package}_FOUND)
    include(${CMAKE_SOURCE_DIR}/../cmssw-cmake/tools/Find${package}.cmake)
#  endif()
elseif(EXISTS ${CMAKE_SOURCE_DIR}/../cmssw-cmake/cmssw/Find${package}.cmake)
#  if(NOT ${package}_FOUND)
    include(${CMAKE_SOURCE_DIR}/../cmssw-cmake/cmssw/Find${package}.cmake)
#  endif()
else()
  list(APPEND _LIBS ${package} )
  list(REMOVE_DUPLICATES _LIBS)
endif()
endmacro()
